{-|
Module      : TTN.View.Article
Description : Code for rendering article-related pages.
Author      : Sam van Herwaarden <samvherwaarden@protonmail.com>
-}

{-# LANGUAGE OverloadedStrings #-}

module TTN.View.Article where

import TTN.Routes

import TTN.Model.Article
import TTN.View.Core

import Control.Monad                    ( forM_ )
import Data.Maybe                       ( fromMaybe )
import Data.Monoid                      ( (<>) )
import Data.Text                        ( Text, pack )
import Lucid
import Text.Digestive.Form
import Text.Digestive.View

import qualified Text.Digestive.Lucid.Html5 as DL

-- | Render the body of an Article or Translation
renderBody :: [[(Int, Text)]] -> Html ()
renderBody b = mapM_ (p_ . toHtml) $ bodyAsParagraphs b

-- * Article views

-- | Generate HTML for new/edit article form
renderArticleForm :: Text -> Token -> View (Html ()) -> Html ()
renderArticleForm target tok view = pageTemplate $
    form_ [method_ "post", action_ target]
          (do DL.errorList "article" view
              inputText_ "article.pub_date" "Publication date (yyyy-mm-dd)" view
              inputText_ "article.title"    "Title"    view
              inputText_ "article.author"   "Author"   view
              inputText_ "article.url"      "URL"      view
              inputText_ "article.summary"  "Summary"  view
              inputText_ "article.language" "Language" view
              inputTextArea_ (Just 25) (Just 100) "article.body" "Body" view
              csrf tok
              submit "Submit article")

-- | Generate HTML for showing an Article
renderArticle :: Article Stored -> Html ()
renderArticle a = do
    p_ . em_ . toHtml $ "Submitted " <> (show $ artCreated a)
    p_ . em_ . toHtml $ "Last edited " <> (show $ artModified a)
    h1_ . toHtml $ artTitle a
    p_ . em_ . toHtml $ (artPubDate a <> " - " <> artAuthor a)
    p_ . a_ [href_ $ artURL a] $ h "Original"
    p_ . a_ [href_ $ editArticlePath a] $ h "Edit"
    maybe (return ()) (p_ . strong_ . toHtml) $ artSummary a
    renderBody $ artBody a
    p_ . toHtml $ "By user " <> (pack . show $ artUID a)
    p_ $ h "Available translations: "
    mapM_ translationLink $ artAvTrans a
    p_ $ h "Translate to:"
    mapM_ translateLink allLanguages
  where translationLink :: Language -> Html ()
        translationLink l = p_ . a_ [href_ $ viewTranslationPath a l] $ toHtml l
        translateLink :: Language -> Html ()
        translateLink l = p_ . a_ [href_ $ newTranslationPath a l] $ toHtml l

-- | For use in listings
renderListArticle :: Article Stored -> Html ()
renderListArticle a =
    div_ (do h3_ . a_ [href_ (viewArticlePath a)] . toHtml $ artTitle a
             p_ . em_ . toHtml $ (artPubDate a <> " - " <> artAuthor a))

renderArticleList :: [Article Stored] -> Html ()
renderArticleList as = do
    p_ . a_ [href_ newArticlePath] $ toHtml ("New article" :: Text)
    mapM_ renderListArticle as

-- * Translation views

-- | Generate HTML for new-Translation form. 'listSubViews' deals with
--   fields generated by 'listOf'. There is some hacky magic going on here
--   for getting the sentences from the original article (they are stored
--   in form fields which do not accept data). (See 'mkTranslateForm' in
--   TTN.Controller.Article)
renderTranslate :: Article Stored
                -> Language
                -> Text
                -> Token
                -> View (Html ())
                -> Html ()
renderTranslate art lang target tok view = pageTemplate $
    form_ [method_ "post", action_ target]
          (do DL.errorList "translate" view
              renderGTranslate (artOrigLang art) lang (artURL art) "GT"
              h2_ $ toHtml ("Title" :: Text)
              p_ . toHtml $ artTitle art
              inputText_ "translate.title" "" view
              h2_ $ toHtml ("Summary" :: Text)
              p_ . toHtml . fromMaybe "" $ artSummary art
              inputText_ "translate.summary" "" view
              h2_ $ toHtml ("Body" :: Text)
              forM_ (listSubViews "translate.body" view) $ \v' ->
                p_ $ forM_ (listSubViews "paragraph" v') $ \v ->
                  do p_ $ DL.label "translation" v (toHtml $ fieldInputText "original" v) 
                     p_ $ DL.inputTextArea (Just 2) (Just 120) "translation" v
                     DL.errorList "translation" v
              csrf tok
              submit "Submit translation")

-- | Generate HTML for showing a translation
renderTranslation :: Article Stored -> Translation -> Html ()
renderTranslation a t = do
    p_ . em_ . toHtml $ "Submitted " <> (show $ trCreated t)
    p_ . em_ . toHtml $ (artPubDate a <> " - " <> artAuthor a)
    h1_ . toHtml $ trTitle t
    p_ . a_ [href_ $ artURL a] . h $ artTitle a
    p_ . a_ [href_ $ viewArticlePath a] $ h "Original (on this site)"
    renderGTranslate (artOrigLang a) (trLang t) (artURL a) "GT"
    maybe (return ()) (p_ . strong_ . toHtml) $ trSummary t
    renderBody $ trBody t

-- * Google Translate URLs

-- | Generate URL for Google Translate version
mkGTranslateURL :: Language -> Language -> Text -> Text
mkGTranslateURL source target article_url =
    "https://translate.google.com/translate?sl=" <> langCode source <>
      "&tl=" <> langCode target <> "&ie=UTF-8&u=" <> article_url

-- | Generate hyperlink
renderGTranslate :: Language -> Language -> Text -> Text -> Html ()
renderGTranslate from to url label =
    p_ . a_ [href_ (mkGTranslateURL from to url)] $ toHtml label

